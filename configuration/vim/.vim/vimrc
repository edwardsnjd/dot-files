" High level
set nocompatible        " Turn of old vi compatibility mode
set modelines=0         " Disable modelines
set encoding=utf-8      " Just make everything boring

" Buffers
set hidden              " Allow unsaved buffers off screen

" General
set showcmd             " Show commands as typing them
set number              " Show line numbers
set relativenumber      " Show relative line numbers
set linebreak           " Break lines at word (requires Wrap lines)
set textwidth=0         " Do not change buffer text when wrapping
set showmatch           " Highlight matching brace
set visualbell          " Use visual bell (no beeping)
set cursorline          " Highlight current line
set title               " Set window title if possible
set shortmess-=S        " Display search match index

set hlsearch            " Highlight all search results
set smartcase           " Enable smart-case search
set ignorecase          " Always case-insensitive

" Tabs
set tabstop=2           " Size of tab in spaces
set shiftwidth=2        " Number of auto-indent spaces
set expandtab           " Insert spaces for tab key
set smartindent         " Enable smart-indent
set softtabstop=0       " Number of spaces per Tab

" Whitespace
set list                " Display whitespace
set listchars=tab:▸\ ,trail:␣

" Aesthetics
set fillchars=vert:│

" Advanced
set undolevels=1000     " Number of undo levels

" Enable code folding
set foldmethod=indent
set nofoldenable        " Do not fold on opening
set foldnestmax=10
set foldlevel=0         " Start folding at this level
set foldminlines=0      " Fold single line regions
let g:markdown_folding = 1

" Show more context
set scrolloff=6

" Enable mouse
set mouse=a

" Splits in more natural location
set splitbelow
set splitright

" Key bindings
let mapleader = " "     " Change default leader from '\'

" Mappings:

" - Make copy consistent with other actions
nnoremap Y y$

" - More convenient way to increase/decrease folding
nnoremap zh zm
nnoremap zl zr

" - Retain selection when indenting
vnoremap < <gv
vnoremap > >gv

" - Save some LH typing
inoremap jk <Esc>

" - Find and select next placeholder
nnoremap <C-j> :keeppattern normal! /\v\<\+[^>]*\+\><C-v><CR><CR>vf><C-g>
smap <C-j> <Esc><C-j>
imap <C-j> <Esc><C-j>

" - Highlight word under cursor but don't jump
nnoremap <silent> * :let @/ = '\<' . expand('<cword>') . '\>' <bar> set hls <cr>
" NOTE: Uses x register
xnoremap <silent> * "xy:<C-U>let @/ = '\V'.escape(@x, '\') <bar> set hls <cr>

" - Refactor: Inline
" NOTE: Uses i and j registers, and i and j marks.
" Implementation deletes the target to @i and copies the variable name to @j
" then sets up a change to replace the variable with the target before deleting
" the declaration line(s).  Much of this is to cater for multiline values.
nmap <silent> <leader>ri viw<leader>ri
xnoremap <silent> <leader>ri 
  \:<C-U>'< mark i<CR>
  \:<C-U>'>+1 mark j<CR>
  \gv
  \"id
  \F=BviW
  \"jy
  \gv
  \:<C-U>let @/ = '\V'.escape(@j, '\') <BAR> set hls <CR>
  \cgni<Esc>
  \:'i,'j-1 delete<CR>
  \n
  \:echo 'Press . to replace each '.@j.' usage with '.@i<CR>

" - Resize current window with arrow keys
nnoremap <Right> :vertical resize +2<CR>
nnoremap <Left> :vertical resize -2<CR>
nnoremap <Up> :resize +2<CR>
nnoremap <Down> :resize -2<CR>

" Normal mode leader mappings:

" - Save buffer
nnoremap <leader>w :w<CR>

" - Close window
nnoremap <leader>x :close<CR>

" - Close buffer
nnoremap <leader>D :bd<CR>

" - Close buffer but keep window
nnoremap <leader>d :bp \| bd#<CR>

" - Cycle through buffers
nnoremap <leader>j :bn<CR>
nnoremap <leader>k :bp<CR>

" - Run the default build command
nnoremap <leader>b :Make<CR>

" - Fuzzy file search in project
nnoremap <leader>p :Files<CR>

" - Fuzzy file search open buffers
nnoremap <leader>P :Buffers<CR>

" - Fuzzy file search recent
nnoremap <leader>H :HISTORY<CR>

" - Fuzzy text search in buffer (in order)
nnoremap <leader>/ :BSLines<CR>

" - Fuzzy text search in buffer for selected text (in order)
" NOTE: Uses x register
xnoremap <leader>/ "xy:<C-U>BSLines =escape(getreg('x'), '\()[]+*{}^$')<CR><CR>

" - Fuzzy tag search in buffer
nnoremap <leader>? :BTags<CR>

" - Fuzzy text search in project
nnoremap <leader>f :RG<CR>

" - Fuzzy text search in project for word under cursor
" NOTE: <C-R><C-W> will not type text that's already there, which will match the
" "b" from the leading "\b"!
nnoremap <leader>F :RG \b<C-R>=expand('<cword>')<CR>\b<CR>

" - Fuzzy text search in project for selected text
" NOTE: Uses x register
xnoremap <leader>F "xy:<C-U>RG <C-R>=escape(getreg('x'), '\()[]+*{}^$')<CR><CR>

" - Display navigable file outline in local list
nnoremap <leader>so <Plug>ShowLocalOutline

" - Display file outline in new buffer
nnoremap <leader>sO <Plug>ShowOutline

" - Display file git history
nnoremap <leader>sg :Redir !git log --oneline -n 1000 -- %<CR>

" - Display file git blame
nnoremap <leader>sb :!git blame -- %<CR>

" - Search devdocs for word under cursor
nnoremap <leader>sd :SearchDevDocs <C-R>=expand('<cword>')<CR><CR>

" - Search devdocs for selected text
" NOTE: Uses x register
xnoremap <leader>sd "xy:<C-U>SearchDevDocs <C-R>=getreg('x')<CR><CR>

" - Search web for word under cursor
nnoremap <leader>sg :SearchWeb <C-R>=expand('<cword>')<CR><CR>

" - Search web for selected text
" NOTE: Uses x register
xnoremap <leader>sg "xy:<C-U>SearchWeb <C-R>=getreg('x')<CR><CR>

" - Search dictionary for word under cursor
nnoremap <leader>sw :SearchDictionary <C-R>=expand('<cword>')<CR><CR>

" - Search thesaurus for word under cursor
nnoremap <leader>st :SearchThesaurus <C-R>=expand('<cword>')<CR><CR>

" - Copy things about current buffer to clipboard
nnoremap <leader>cf :let @+ = expand("%") <BAR> redraw <BAR> echo 'Copied path to clipboard'<CR>

" - Quickly copy current file (leaves you in command mode)
nnoremap <leader>cp :! cp % %:h/

" - Open in Github
nnoremap <leader>- :GetCurrentBranchLink<CR>

" - Preview markdown
nnoremap <leader>m :PreviewMarkdown<CR><CR>

" - Visual mode quick duplicate
xnoremap D y'>pgv

" - Manually manage location list
nmap <leader>la <Plug>(LociAdd)
nmap <leader>lc <Plug>(LociClear)
nmap <leader>lr <Plug>(LociRead)
nmap <leader>lw <Plug>(LociWrite)

" - Writing presentations
nnoremap <leader>th :.!toilet -d ~/figlet-fonts -f big -W -w 300<CR>
nnoremap <leader>tH :.!toilet -d ~/figlet-fonts -f standard -W -w 300<CR>
nnoremap <leader>tf :.!toilet -f future -W -w 300<CR>
nnoremap <leader>tF :.!toilet -f pagga -W -w 300<CR>
nnoremap <leader>tb :.!toilet -f term -F border -w 300<CR>

" - Managing todo lists
nnoremap <leader>ti :put ='- [ ] <+ TODO +>'<CR>
nnoremap <leader>tt :keeppatterns s/\v\[([^\]]*)\]/\="[".(submatch(1)==" "?"x":" ")."]"/<CR>

" - Show presentation
noremap <silent> <F5> :silent call <SID>Present()<CR>
function! <SID>Present()
  set noruler noshowcmd norelativenumber nonumber noshowmode nohidden nocursorline
  set listchars= laststatus=0 shortmess=F
  set scrolloff=0
  GitGutterDisable
  nnoremap <silent> <Right> :bn<CR>
  nnoremap <silent> <Left> :bp<CR>
  nnoremap <Up> <nop>
  nnoremap <Down> <nop>
endfunc

" - Show highlight groups at the cursor
" See: https://vim.fandom.com/wiki/Identify_the_syntax_highlighting_group_used_at_the_cursor
nmap <silent> <F10> :echo 
\ 'hi<'    . synIDattr(synID(line("."),col("."),1),"name") . '> ' .
\ 'trans<' . synIDattr(synID(line("."),col("."),0),"name") . '> ' .
\ 'lo<'    . synIDattr(synIDtrans(synID(line("."),col("."),1)),"name") . '>'<CR>

" Show current syntax
noremap <leader>ss :call <SID>SynStack()<CR>
function! <SID>SynStack()
  if !exists("*synstack")
    return
  endif
  echo map(synstack(line('.'), col('.')), 'synIDattr(v:val, "name")')
endfunc

" Fix syntax highlighting it goes wonky
nnoremap <F12> <Esc>:syntax sync fromstart<CR>
inoremap <F12> <C-o>:syntax sync fromstart<CR>

" .vimrc shortcuts
nnoremap <leader>ve :edit $MYVIMRC<CR>
nnoremap <leader>vr :source $MYVIMRC <BAR> redraw <BAR> echo 'Reloaded .vimrc'<CR>

" Github cli shortcuts
nnoremap <leader>gp :! gh pr create<CR>
nnoremap <leader>gs :! gh pr status<CR>
nnoremap <leader>gc :! gh pr checks<CR>

" tmux
nnoremap <leader>ter :silent . write ! tmux-send .right<CR>
xnoremap <leader>ter :silent . write ! tmux-send .right<CR>
nnoremap <leader>ty :silent . write ! tmux load-buffer -<CR>:echo 'Copied to tmux'<CR>
nnoremap <leader>tp :    read ! tmux show-buffer<CR>:echo 'Pasted from tmux'<CR>
nnoremap <leader>tP :.-1 read ! tmux show-buffer<CR>:echo 'Pasted from tmux'<CR>

" - Execute lines and append output
" Uses x register.
nnoremap <leader>eeb "xyy"xPj!!bash<CR>
xnoremap <leader>eeb "xy"xo<Esc>p!!bash<CR>gv
nnoremap <leader>eer "xyy"xPj!!ruby<CR>
xnoremap <leader>eer "xy"xo<Esc>p!!ruby<CR>gv
nnoremap <leader>eep "xyy"xPj!!python3<CR>
xnoremap <leader>eep "xy"xo<Esc>p!!python3<CR>gv

" - Execute current line (in shell)
nnoremap <leader>es :!<C-R>=escape(getline('.'), '%')<CR><CR>
nnoremap <leader>eS :!clear;<C-R>=escape(getline('.'), '%')<CR><CR>
vnoremap <leader>es :w !sh<CR>
vnoremap <leader>eS :w !sh<CR>

" Snippets
nnoremap <leader>is :call <SID>Snippet()<cr>
function! <SID>Snippet() abort
  let l:tempname = tempname()
  try
    execute 'silent ! snippet ' . &filetype . ' > ' . fnameescape(l:tempname)
    " Insert text returned into file below cursor position
    execute 'read ' . l:tempname
    redraw!
  finally
    call delete(l:tempname)
  endtry
endfunction

" Insert person from git
vnoremap <leader>ip :call <SID>ReplaceWithGitPerson()<CR>
function! <SID>ReplaceWithGitPerson() abort
  let l:selected = <SID>GitContributor()
  execute "normal! gvc\<C-r>=l:selected\<cr>"
  redraw!
endfunction
" Select person from git
function! <SID>GitContributor() abort
  let l:tempname = tempname()
  try
    execute 'silent ! git contributor > ' . fnameescape(l:tempname)
    " Return any person selected (without new line)
    let l:output = readfile(l:tempname, '', 1)
    return len(l:output) > 0 ? l:output[0] : v:null
  finally
    call delete(l:tempname)
  endtry
endfunction

" fzf mappings:

" - Display mappings
nmap <leader><tab> <plug>(fzf-maps-n)
xmap <leader><tab> <plug>(fzf-maps-x)
omap <leader><tab> <plug>(fzf-maps-o)

" - Insert mode completion
" imap <c-x><c-k> <plug>(fzf-complete-word)
" imap <c-x><c-f> <plug>(fzf-complete-path)
" imap <c-x><c-l> <plug>(fzf-complete-line)

" - Enlarge default fzf popup
let g:fzf_layout = { 'window': { 'width': 0.95, 'height': 0.9 } }
" - Preview above the list
let g:fzf_preview_window = ['up:40%:border-bottom', 'ctrl-/']

" General file editing enhancements
filetype indent plugin on

" Specific file types
autocmd Filetype gitcommit setlocal textwidth=80
autocmd Filetype ruby setlocal colorcolumn=120
autocmd Filetype javascript setlocal colorcolumn=91
autocmd Filetype typescript setlocal colorcolumn=91
autocmd Filetype python setlocal colorcolumn=120

" Markdown
autocmd Filetype markdown noremap j gj
autocmd Filetype markdown noremap k gk
autocmd! User GoyoEnter Limelight
autocmd! User GoyoLeave Limelight!

" Prefer ripgrep over grep if available
if executable('rg')
  set grepprg=rg\ --vimgrep\ --no-heading\ --smart-case\ --hidden
endif

" New commands

" Improve :Rg command to include hidden files
command! -bang -nargs=* RG call fzf#vim#grep(
\ "rg --column --line-number --no-heading --color=always --smart-case --hidden -- ".shellescape(<q-args>),
\ 1, fzf#vim#with_preview({'options': ['--exact']}), <bang>0)

" Alternate :BLines command with all lines in order
command! -bang -nargs=* BSLines call fzf#vim#buffer_lines(<q-args>,
\ { 'options': ['--no-sort', '--query', len(split(<q-args>)) ? split(<q-args>)[0].' ' : '' ] },
\ <bang>0)

" Alternate :History command with all lines in order
command! -bang -nargs=* HISTORY call fzf#vim#history({ 'options': ['--no-sort'] }, <bang>0)

" Preview markdown (OSX using pandoc)
command PreviewMarkdown ! NE_MD_OUT_FILE="${TMPDIR}%:t.html" && pandoc -s --mathjax "%" > "$NE_MD_OUT_FILE" && open "$NE_MD_OUT_FILE"

command! -nargs=* SearchWeb silent exec "! ddg ".shellescape(<q-args>) | redraw!
command! -nargs=* SearchDevDocs silent exec "! open https://devdocs.io/\\#q=".shellescape(<q-args>) | redraw!
command! -nargs=* SearchDictionary exec "! define ".shellescape(<q-args>)
command! -nargs=* SearchThesaurus exec "! thesaurus ".shellescape(<q-args>)

" Manually remembering list of locations
nnoremap <Plug>(LociAdd) :caddexpr GetLocation()<CR>
nnoremap <Plug>(LociClear) :cexpr [] <bar> cclose<CR>
nnoremap <Plug>(LociRead) :ReadLociFile 
nnoremap <Plug>(LociWrite) :WriteLociFile 

function! GetLocation()
  return expand('%') . ':' . line('.') . ':' . col('.') . ':' . '['.input('Label (optional): ').'] '. getline('.')
endfunction

" See: https://www.reddit.com/r/vim/comments/9iwr41/store_quickfix_list_as_a_file_and_load_it/
command! -complete=file -nargs=1 ReadLociFile call <SID>ReadLociFile(<f-args>)
function! <SID>ReadLociFile(file) abort
  let l:efm = &l:efm
  let &l:errorformat = "%-G%f:%l: All of '%#%.depend'%.%#,%f%.%l col %c%. %m"
  execute 'cfile '.a:file
  let &l:efm = l:efm
endfunction

command! -complete=file -nargs=1 WriteLociFile call <SID>WriteLociFile(<f-args>)
function! <SID>WriteLociFile(file) abort
  copen
  execute 'write! '.a:file
endfunction

" Redirect the output of a Vim or external command into a scratch buffer
" https://gist.github.com/romainl/eae0a260ab9c135390c30cd370c20cd7
" Update via:
"   :/Redir::Start/+;/Redir::End/- ! curl --silent --location "https://gist.github.com/romainl/eae0a260ab9c135390c30cd370c20cd7/raw/ce546f5cdbb954fecc4b0120e3431b64f68d0373/redir.vim" | sed -E '/command\!/ { /-bar/ \! s/^/" /; }'
"   :'[,'] retab
" ----- Redir::Start
function! Redir(cmd, rng, start, end)
  for win in range(1, winnr('$'))
    if getwinvar(win, 'scratch')
      execute win . 'windo close'
    endif
  endfor
  if a:cmd =~ '^!'
    let cmd = a:cmd =~' %'
      \ ? matchstr(substitute(a:cmd, ' %', ' ' . shellescape(escape(expand('%:p'), '\')), ''), '^!\zs.*')
      \ : matchstr(a:cmd, '^!\zs.*')
    if a:rng == 0
      let output = systemlist(cmd)
    else
      let joined_lines = join(getline(a:start, a:end), '\n')
      let cleaned_lines = substitute(shellescape(joined_lines), "'\\\\''", "\\\\'", 'g')
      let output = systemlist(cmd . " <<< $" . cleaned_lines)
    endif
  else
    redir => output
    execute a:cmd
    redir END
    let output = split(output, "\n")
  endif
  vnew
  let w:scratch = 1
  setlocal buftype=nofile bufhidden=wipe nobuflisted noswapfile
  call setline(1, output)
endfunction

" This command definition includes -bar, so that it is possible to "chain" Vim commands.
" Side effect: double quotes can't be used in external commands
command! -nargs=1 -complete=command -bar -range Redir silent call Redir(<q-args>, <range>, <line1>, <line2>)

" This command definition doesn't include -bar, so that it is possible to use double quotes in external commands.
" Side effect: Vim commands can't be "chained".
" command! -nargs=1 -complete=command -range Redir silent call Redir(<q-args>, <range>, <line1>, <line2>)
" ----- Redir::End

" Plugins (managed by `vim-plugin`)
" See `README.md` for bootstrap

call plug#begin('~/.vim/plugged')
Plug 'https://github.com/chrisbra/matchit'
Plug 'https://github.com/tpope/vim-sensible'
Plug 'https://github.com/tpope/vim-fugitive'
Plug 'https://github.com/tpope/vim-vinegar'
Plug 'https://github.com/tpope/vim-surround'
" Plug 'https://github.com/tpope/vim-rails'
Plug 'https://github.com/tpope/vim-repeat'
Plug 'https://github.com/tpope/vim-projectionist'
Plug 'https://github.com/tpope/vim-dispatch'
Plug 'https://github.com/tpope/vim-unimpaired'
Plug 'https://github.com/vim-airline/vim-airline'
Plug 'https://github.com/vim-airline/vim-airline-themes'
Plug 'https://github.com/airblade/vim-gitgutter'
Plug 'https://github.com/sheerun/vim-polyglot'
Plug 'https://github.com/haishanh/night-owl.vim'
Plug 'https://github.com/junegunn/fzf'
Plug 'https://github.com/junegunn/fzf.vim'
Plug 'https://github.com/knsh14/vim-github-link'
Plug 'https://github.com/junegunn/goyo.vim'
Plug 'https://github.com/junegunn/limelight.vim'
Plug 'https://github.com/wincent/terminus'
Plug 'https://github.com/psf/black'
Plug 'https://github.com/editorconfig/editorconfig-vim'
Plug 'https://github.com/mbbill/undotree.git'
Plug 'https://github.com/edwardsnjd/vim-lsp', { 'branch': 'before-init' }
call plug#end()

" Configure vim-lsp

" Show errors inline
let g:lsp_diagnostics_echo_cursor = 1

" Tweak the initialisation params to null the process id
" to allow for LSP servers running in a different process
" namespace to this client
function! <SID>NullifyProcess(init_params) abort
  let a:init_params['processId'] = v:null
  return a:init_params
endfunction

if executable('lsp-python')
  autocmd User lsp_setup call lsp#register_server({
        \   'name': 'lsp-python',
        \   'cmd': { server_info->['lsp-python'] },
        \   'allowlist': ['python'],
        \ })
endif

if executable('lsp-csharp')
  autocmd User lsp_setup call lsp#register_server({
        \   'name': 'lsp-csharp',
        \   'cmd': { server_info->['lsp-csharp'] },
        \   'allowlist': ['cs'],
        \   'before_init': function('<SID>NullifyProcess'),
        \ })
endif

if executable('lsp-json')
  autocmd User lsp_setup call lsp#register_server({
        \   'name': 'lsp-json',
        \   'cmd': { server_info->['lsp-json'] },
        \   'allowlist': ['json'],
        \   'before_init': function('<SID>NullifyProcess'),
        \ })
endif

if executable('solargraph')
  autocmd User lsp_setup call lsp#register_server({
        \   'name': 'solargraph',
        \   'cmd': { server_info->['solargraph', 'stdio'] },
        \   'allowlist': ['ruby'],
        \   'initialization_options': {
        \     'completion': 'true',
        \     'hover': 'true',
        \     'symbols': 'true',
        \     'definitions': 'true',
        \     'rename': 'true',
        \     'references': 'true',
        \     'diagnostics': 'true',
        \     'formatting': 'true',
        \     'folding': 'true',
        \     'highlights': 'true',
        \   },
        \ })
elseif executable('lsp-ruby')
  autocmd User lsp_setup call lsp#register_server({
        \   'name': 'lsp-ruby',
        \   'cmd': { server_info->['lsp-ruby'] },
        \   'allowlist': ['ruby'],
        \   'initialization_options': {
        \     'completion': 'true',
        \     'hover': 'true',
        \     'symbols': 'true',
        \     'definitions': 'true',
        \     'rename': 'true',
        \     'references': 'true',
        \     'diagnostics': 'true',
        \     'formatting': 'true',
        \     'folding': 'true',
        \     'highlights': 'true',
        \   },
        \ })
endif

if executable('typescript-language-server')
  autocmd User lsp_setup call lsp#register_server({
        \   'name': 'typescript-language-server',
        \   'cmd': { server_info->['typescript-language-server', '--stdio'] },
        \   'root_uri': { server_info->lsp#utils#path_to_uri(lsp#utils#find_nearest_parent_directory(lsp#utils#get_buffer_path(), '.git/..')) },
        \   'allowlist': ['javascript', 'typescript', 'typescriptreact'],
        \   'before_init': function('<SID>NullifyProcess'),
        \ })
elseif executable('lsp-typescript')
  autocmd User lsp_setup call lsp#register_server({
        \   'name': 'lsp-typescript',
        \   'cmd': { server_info->['lsp-typescript'] },
        \   'root_uri': { server_info->lsp#utils#path_to_uri(lsp#utils#find_nearest_parent_directory(lsp#utils#get_buffer_path(), '.git/..')) },
        \   'allowlist': ['javascript', 'typescript', 'typescriptreact'],
        \   'before_init': function('<SID>NullifyProcess'),
        \ })
endif

if executable('lsp-rust')
  autocmd User lsp_setup call lsp#register_server({
        \   'name': 'lsp-rust',
        \   'cmd': { server_info->['lsp-rust'] },
        \   'whitelist': ['rust'],
        \   'initialization_options': {
        \     'cargo': {
        \       'loadOutDirsFromCheck': v:true,
        \     },
        \     'procMacro': {
        \       'enable': v:true,
        \     },
        \   },
        \ })
endif

if executable('kotlin-language-server')
    autocmd User lsp_setup call lsp#register_server({
        \ 'name': 'kotlin-language-server',
        \ 'cmd': {server_info->[&shell, &shellcmdflag, 'kotlin-language-server']},
        \ 'whitelist': ['kotlin']
        \ })
endif

function! <SID>ConfigureLsp() abort
  " Misc
  setlocal signcolumn=yes
  let g:lsp_format_sync_timeout = 1000

  " C-X C-O for LSP autocomplete
  setlocal omnifunc=lsp"complete

  " Use LSP not ctags
  if exists('+tagfunc')
    setlocal tagfunc=lsp#tagfunc
  endif

  " Informational
  nmap <buffer> K <plug>(lsp-hover)
  nmap <buffer> [g <plug>(lsp-previous-diagnostic)
  nmap <buffer> ]g <plug>(lsp-next-diagnostic)
  nmap <buffer> gS <plug>(lsp-workspace-symbol-search)
  nmap <buffer> gd <plug>(lsp-definition)
  nmap <buffer> ge <plug>(lsp-document-diagnostics)
  nmap <buffer> gp <plug>(lsp-peek-definition)
  nmap <buffer> gr <plug>(lsp-references)
  nmap <buffer> gs <plug>(lsp-document-symbol-search)
  " Mutating
  nmap <buffer> <leader>rf <plug>(lsp-document-format)
  nmap <buffer> <leader>rn <plug>(lsp-rename)
  nmap <buffer> <leader>rr <plug>(lsp-code-action)
  vmap <buffer> <leader>rr :LspCodeAction<CR>
endfunction

augroup lsp_install
  autocmd!
  autocmd User lsp_buffer_enabled call <SID>ConfigureLsp()
augroup END

" enable 24bit true color
if (has("termguicolors"))
  set termguicolors
endif

" Expect terminal emulator to display things quickly
if (has("termguicolors"))
  set ttyfast
endif

" Correct italics
let &t_ZH="\e[3m"
let &t_ZR="\e[23m"

" enable the theme
colorscheme night-owl
let g:airline_theme='night_owl'

" Tweak the night-owl colours:
" hi Normal guifg=#d6deeb ctermfg=253 guibg=#011627 ctermbg=233 gui=NONE cterm=NONE
" hi Comment guifg=#637777 ctermfg=243 gui=italic cterm=italic
" hi IncSearch guifg=#eeeeee ctermfg=255 guibg=#ecc48d ctermbg=222 gui=NONE cterm=NONE
" hi Search guifg=#011627 ctermfg=233 guibg=#ecc48d ctermbg=222 gui=NONE cterm=NONE
" hi Todo guifg=#777777 ctermfg=243 guibg=#ecc48d ctermbg=222 gui=NONE cterm=NONE
" hi SpecialKey guifg=#ecc48d ctermfg=222 gui=NONE cterm=NONE
" hi diffAdded guifg=#addb67 ctermfg=149 gui=NONE cterm=NONE
" hi diffRemoved guifg=#ff5874 ctermfg=204 gui=NONE cterm=NONE
" hi ColorColumn guibg=#222222 ctermbg=235 gui=NONE cterm=NONE
" - Tone down the search highlighting
hi IncSearch  guibg=#a3cccc guifg=#607080
hi Search     guibg=#404040 guifg=#d6deeb
" - Tone down the TODO highlight
hi Todo       guibg=#011627 guifg=#93aaaa
" - Tone down whitespace (and vim mappings)
hi SpecialKey               guifg=#a8826b
" - Play with diffs
hi diffAdded   guifg=#40dd40
hi diffRemoved guifg=#dd5874
" - Subtle column marker
hi ColorColumn guibg=#011728
" - Add highlight for column, to match cursor line
hi CursorColumn guibg=#112630

" Trying to use Night Owl terminal colours
let g:terminal_ansi_colors = [
      \ g:terminal_color_0,
      \ g:terminal_color_1,
      \ g:terminal_color_2,
      \ g:terminal_color_3,
      \ g:terminal_color_4,
      \ g:terminal_color_5,
      \ g:terminal_color_6,
      \ g:terminal_color_7,
      \ g:terminal_color_8,
      \ 'red',
      \ g:terminal_color_10,
      \ g:terminal_color_11,
      \ g:terminal_color_12,
      \ g:terminal_color_13,
      \ g:terminal_color_14,
      \ g:terminal_color_15
      \]

" Make netrw (file explorer) look sane
let g:netrw_liststyle = 3     " Nested view
let g:netrw_banner = 0        " No banner
let g:netrw_winsize = 20      " 20% width
